typ int : integer;

# iterative fibonacci
fun fib2(x : integer) : integer = (
	{fn = 1},
	{f1 = 1}, 
	{f2 = 1}, 
	{for i = 2, x, 1 : (
		{fn = f1 + f2}, 
		{f1 = f2}, 
		{f2 = fn}, 
		0
		)
	},
	fn
) {where var fn : integer; var f1 : integer; var f2 : integer; var i : integer};

# recursicve fibonacci
fun fib(x : integer) : integer = (
	({ if x < 2 then 
		{rez = 1} 
	else 
		{rez = fib(x - 1) + fib(x - 2)}}, rez) 
	{where var rez : integer}
);

var tab : arr[10] integer;
fun bubble(x : integer) : integer = (
	{for i = 0, x, 1 : 
		{for j = 0, x, 1: 
			{if tab[j] > tab[i] then 
				(
				{tmp = tab[j]},
				{tab[j] = tab[i]},
				{tab[i] = tmp},0
				) 
				{ where var tmp : integer} 
			}
		}
	},
	{ for i = 0, x, 1: putInt(tab[i]) },
	1
) { where var i : integer; var j : integer };

# binary search
fun binSearch(begin : integer, end:integer, x : integer) : logical = (
	{ if end < begin then { result = false } 
	else
		({ pivot = begin + (end - begin) / 2 },
		{
		if tab[pivot] == x then 
			{result = true} 
		else 
			{
			if tab[pivot] < x then
				{result = binSearch(pivot + 1, end, x)}
			else
				{result = binSearch(begin, pivot - 1, x)}
			}
		})
	},
	result
) { where var pivot : integer; var result : logical };


# factorial
fun fact(x : integer) : integer = (
	{ if x == 0 then 
		{res = 1} 
	else
		{ if x == 1 then 
			{ res = 1 }
		else 
			{ res = fact(x - 1) * x }
		}
	},
	res
) { where var res : integer };


# gcd
fun gcd(x:integer, y:integer) : integer = (
	{ res = x },
	{ if y > 0 then {res = gcd(y, x % y) }},
	res
) { where var res : integer  }